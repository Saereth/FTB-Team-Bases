plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    // needs to be done AFTER version is set
    apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/publishing.gradle"

    repositories {
        mavenLocal()
        maven {
            url "https://maven.architectury.dev/"
            content {
                includeGroup "dev.architectury"
            }
        }

        maven {
            url "https://maven.ftb.dev/releases"
            content {
                includeGroup "dev.ftb.mods"
            }
        }

        maven {
            url "https://maven.ftb.dev/snapshots"
            content {
                includeGroup "dev.ftb.mods"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "maven-publish"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    }

    publishing {
        repositories {
            if (ftbPublishing.ftbToken) {
                maven {
                    url ftbPublishing.ftbURL
                    credentials {
                        username = ftbPublishing.ftbUser
                        password = ftbPublishing.ftbToken
                    }
                }
            }

//            if (ftbPublishing.sapsToken) {
//                maven {
//                    url ftbPublishing.sapsURL
//                    credentials {
//                        username = ftbPublishing.sapsUser
//                        password = ftbPublishing.sapsToken
//                    }
//                }
//            }
        }
    }
}

task curseforgePublish
